buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "Minecraft Forge"
            url = "http://files.minecraftforge.net/maven"
        }
		 maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

// define the properties file
ext.configFile = file "build.prop"

configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'forge'
    apply plugin: 'maven'
    group = config.group_name
    version = config.mod_version
	archivesBaseName = "[${config.minecraft_version}]LeatherWaterSac-TFC+"

    minecraft {
        version = config.minecraft_version + "-" + config.forge_version
        if(file('../run').exists()) {
            runDir = "../run"
        } else {
            runDir = "run"
            if (!file(runDir).exists()) file(runDir).mkdir()
        }
    }
}

//Creates a compiled de-obfuscated jar
task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
}

//creates a jar containing only the src
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

tasks.build.dependsOn('sourceJar', 'deobfJar')

dependencies {
     compile fileTree(dir:'libs',include:'*.jar')
}

processResources {
	// First we just copy in the mcmod.info file
	from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.prop'

        // replaces
        expand ([
            'mod_version': project.config.mod_version,
            'minecraft_version': project.config.minecraft_version,
        ])
    }
    // copy everything else, that we didn't copy before
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.prop'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
